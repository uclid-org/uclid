module queue
{
  type op_t = enum { push, pop, search };
  type index_t = integer;
  type count_t = integer;
  type data_t = integer;
  type queue_t = [index_t]data_t;

  const SIZE : integer = 4;

  var contents      : queue_t;
  var head, tail    : index_t;
  var count         : count_t;
  var inited        : boolean;

  input op          : op_t;
  input data        : integer;

  output first      : data_t;
  output valid      : boolean;
  output full       : boolean;
  output empty      : boolean;
  output found      : boolean;

  define itemIndex(i : index_t) : index_t =
    if ((i + head) >= SIZE)
        then (i + head) - SIZE
        else (i + head);
  define in_queue(v : integer) : boolean =
    (exists (i : index_t) :: 
        (i >= 0 && i < count) && contents[itemIndex(i)] == v);

  init {
    head   = 0;
    tail   = 0;
    count  = 0;
    valid  = false;
    full   = false;
    empty  = true;
    inited = false;
    found  = false;
  }

  procedure [noinline]pushQ()
    returns (
        contentsP : queue_t,
        tailP : index_t,
        countP : integer)

    requires !full;
    requires (tail >= 0 && tail < SIZE);
    ensures (contentsP == contents[tail -> data]);
    ensures (tailP == if (tail == SIZE-1) then 0 else (tail + 1));
    ensures (countP == count + 1);
  {
    contentsP = contents[tail -> data];
    tailP = tail + 1;
    if (tailP >= SIZE) { tailP = 0; }
    countP = count + 1;
  }

  procedure [noinline] popQ()
    returns (
        headP : integer,
        countP : integer)

    requires !empty;
    requires (head >= 0 && head < SIZE);
    requires (count >0);
    ensures (headP == if (head == SIZE-1) then 0 else (head + 1));
    ensures (countP == count - 1);
  {
    headP = head + 1;
    if (headP >= SIZE) { headP = 0; }
    countP = count - 1;
  }

  procedure [noinline]searchQ()
    returns (found : boolean)
    requires (count >= 0 && count <= SIZE);
    ensures (in_queue(data) <==> found);
  {
    var i : integer;

    i = 0;
    found = false;
    while (i < count)
      invariant (i >= 0 && i <= count) && 
        (exists (j : integer) :: j >= 0 && j < i && contents[itemIndex(j)] == data) <==> found;
    {
      if (contents[itemIndex(i)] == data) {
        found = true;
      }
      i = i + 1;
    }
  }

  next {
    inited' = true;
    first'  = contents[head];
    full'   = (count' == SIZE);
    empty'  = (count' == 0);

    case
      (op == push && !full) : {
        call (contents', tail', count') = pushQ();
      }
      (op == pop && !empty) : {
        call (head', count') = popQ();
      }
      (op == search) : {
        call (found') = searchQ();
      }
    esac;
  }

  const pushed_data : data_t;

  // count is always between 0 and SIZE.
  invariant count_valid:
    (count >= 0 && count <= SIZE) &&
    (head >= 0 && head < SIZE) &&
    (tail >= 0 && tail < SIZE) 
    && (count==SIZE ==> full)
    && (count==0 ==> empty);

  // // invariant[LTL] queue_inserts :
  //   // if we push something
  //   G((inited && op == push && X(op != pop) &&
  //      data == pushed_data && !full)
  //   // it enters the queue
  //   ==> X(in_queue(pushed_data))) &&

  // // invariant[LTL] push_eventually_pops :
  //   // no NOPs
  //   G(op != search)             ==>
  //   // no push when full
  //   G(op == push ==> !full)     ==>
  //   // no pop when empty
  //   G(op == pop  ==> !empty)    ==>
  //   // everything that is pushed will eventually be popped.
  //   G((inited && op == push && data == pushed_data) ==>
  //     F(op == pop && first == pushed_data)) &&

  // // invariant[LTL] pushed_value_becomes_head :
  //   // there is always eventually a push
  //   G(F(op == pop)) ==>
  //   // every value that is pushed
  //   G((inited && op == push && data == pushed_data && !full) ==>
  //     // eventually becomes the head of the queue
  //     F(first == pushed_data));

  control {
    vQ      = induction;
    // vPush   = verify(pushQ);
    // vPop    = verify(popQ);
    // vSearch = verify(searchQ);
    check;
    print_results;
    vQ.print_cex(
        op, data, pushed_data,
        head, tail, first, count,
        contents[0], contents[1],
        contents[2], contents[3], SIZE);
  }
}
