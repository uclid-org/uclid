module foo
{
  input a : integer;
  output b : integer;
  
  next {
    b' = a + 1;
  }
}

module bar
{
  input x : integer;
  input y : integer;
  output z : integer;
  
  var xp, yp : integer;

  instance foox : foo(a:(x), b:(xp));
  instance fooy : foo(a:(y), b:(yp));
  
  next {
    next(foox);
    next(fooy);
    z' = xp + yp;
  }
}

module main
{
  input x   : integer;
  output y  : integer;
  var initd : boolean;
  
  instance bar1 : bar(x: (x), y: (x), z: (y));
  
  init { initd = false; } 

  next {
    initd' = true;
    next(bar1);
  }
  
  // bar1.foox.a is x, bar1.fooy.a is x. 
  // y is bar1.z, which is bar1.foox.b + bar1.fooy.b
  // This property says that x + 1 + x + 1 == y
  property b1: initd ==> (bar1.foox.a + 1 + bar1.fooy.a + 1 == y);

  control {
      v = unroll(1);
      check;
      print_results;
      v.print_cex;
      print_module;
  }
}
